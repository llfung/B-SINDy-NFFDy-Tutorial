## Data assimilation to estimate parameters of the Lotka-Volterra model using Turing.jl
# Directly adapted from the Turing.jl documentation: https://turinglang.org/docs/tutorials/10-bayesian-differential-equations/

## Initialisation
using Turing, DifferentialEquations, LinearAlgebra, Distributions

# Load StatsPlots for visualizations and diagnostics.
using StatsPlots

## Forward problem - Lotka-Volterra model
# Define Lotka-Volterra model.
function lotka_volterra(du, u, p, t)
    # Model parameters.
    α, β, γ, δ = p
    # Current state.
    x, y = u

    # Evaluate differential equations.
    du[1] = (α - β * y) * x # prey
    du[2] = (δ * x - γ) * y # predator

    return nothing
end

# Define initial-value problem.
u0 = [1.0, 1.0]
p = [1.5, 1.0, 3.0, 1.0]
tspan = (0.0, 10.0)
prob = ODEProblem(lotka_volterra, u0, tspan, p)

# Plot simulation.
sol = solve(prob, Tsit5(); saveat=0.1)
odedata = Array(sol) + 0.8 * randn(size(Array(sol)))

# Plot simulation and noisy observations.
plot(sol; alpha=0.3)
scatter!(sol.t, odedata'; color=[1 2], label="")

## Inverse problem - Bayesian inference
@model function fitlv(data, prob)
    # Prior distributions.
    σ2 ~ InverseGamma(2, 3)
    α ~ truncated(Normal(1.5, 0.5); lower=0.5, upper=2.5)
    β ~ truncated(Normal(1.2, 0.5); lower=0, upper=2)
    γ ~ truncated(Normal(3.0, 0.5); lower=1, upper=4)
    δ ~ truncated(Normal(1.0, 0.5); lower=0, upper=2)

    # Simulate Lotka-Volterra model. 
    p = [α, β, γ, δ]
    predicted = solve(prob, Tsit5(); p=p, saveat=0.1)

    # Observations.
    for i in 1:length(predicted)
        data[:, i] ~ MvNormal(predicted[i], σ2 * I)
    end

    return nothing
end

model = fitlv(odedata, prob)

# Sample with forward-mode automatic differentiation (the default).
chain = sample(model, NUTS(), 1000; progress=true)

## Data Retrodiction
plot(; legend=false)
posterior_samples = sample(chain[[:α, :β, :γ, :δ]], 300; replace=false)
for p in eachrow(Array(posterior_samples))
    sol_p = solve(prob, Tsit5(); p=p, saveat=0.1)
    plot!(sol_p; alpha=0.1, color="#BBBBBB")
end

# Plot simulation and noisy observations.
plot!(sol; color=[1 2], linewidth=1)
scatter!(sol.t, odedata'; color=[1 2])

## Using MAP instead
map_estimate = maximum_a_posteriori(model,initial_params=mean(chain).nt.mean)
StatsBase.coeftable(map_estimate)